/**
 * Designed and developed by Kevlar Contributors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.buildconfig.internal.model.ModuleArtifactCoordinates
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

/**
 * Module identifier, used across the project to reference this module.
 * This should match the directory name this file is in.
 * */
ext.module_name = "integrity"

/**
 * Deploying Coordinates for local module.
 *
 * You should modify (ona a per-module basis):
 *   - PUBLISH_GROUP_ID
 *   - PUBLISH_ARTIFACT_ID
 *   - PUBLISH_ARTIFACT_DESC
 *   - PUBLISH_ARTIFACT_WEBSITE
 *   - PUBLISH_VERSION
 * Accordingly to your library publication artifact(s)
 * */

project.ext.set("module_${ext.module_name}", new ModuleArtifactCoordinates(
        // PUBLISH_GROUP_ID
        "io.github.kevlar-kt",
        
        // PUBLISH_ARTIFACT_ID
        "integrity",
        
        // PUBLISH_VERSION
        "1.1.1",
        
        // PUBLISH_ARTIFACT_DESC
        "Kevlar Integrity",
        
        // PUBLISH_ARTIFACT_WEBSITE
        "https://github.com/kevlar-kt/kevlar"
))

// Android library script
apply from: rootProject.file("scripts/commons/library_config.gradle")


/**
 * Library versioning
 * */
android {
    defaultConfig {
        versionName "1.1.1-kevlar-integrity"
        versionCode 1
    }
    namespace 'com.kevlar.integrity'
}


/**
 * Library dependencies
 * */
dependencies {
    // Kotlin dependencies
    compileOnly "androidx.annotation:annotation:1.7.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    
    // Testing dependencies
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test:core:1.5.0"
    testImplementation "org.mockito:mockito-core:5.9.0"
    testImplementation "com.google.truth:truth:1.2.0"
    testImplementation "org.robolectric:robolectric:4.11.1"
}


tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        // Explicit API Mode
        freeCompilerArgs += [
                //'-progressive',
                '-Xexplicit-api=strict',
                //'-opt-in=kotlin.RequiresOptIn',
        ]
    }
}


// Publishing file
apply from: "${rootDir}/scripts/publishing/publish-module-integrity.gradle"